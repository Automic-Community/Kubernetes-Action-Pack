*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_resource_scenarios
Test Template     Delete Resource Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ResourceType      ResourceName         Otheroptions
Delete Resource with valid Resource Type , Resource name and not giving other options
                      [Tags]    main_scenarios
                      OK        Services          nginx-deployment     ${EMPTY}

Delete Resource with valid Resource Type , Resource name and giving other options
                      OK        deployment        nginx-deployment     --timeout=0s

Delete Resource with Invalid Resource Type
                      NOK       Invalid           nginx-deployment1    ${EMPTY}

Delete Resource with Invalid Resource Name
                      NOK       deployment        Invalid              ${EMPTY}

Delete Resource with Invalid Other Options
                      NOK       deployment        nginx-deployment1    Invalid

Delete Resource with already deleted Resource
                      NOK       deployment        nginx-deployment     ${EMPTY}

Assert File Location as Resource Type
                      NOK       ${EMPTY}          nginx-deployment     --timeout=0s

Assert File Location as Resource Name
                      NOK       deployment        ${EMPTY}             --timeout=0s

*** Keywords ***
Startup
    Log    *******Start Test "DELETE RESOURCE"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Resource Template
    [Arguments]    ${OK_NOK}    ${ResourceType}    ${ResourceName}    ${Otheroptions}
    [Documentation]    This test executes the Delete Resource action.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${ResourceType}: This field specifies Ytype of the resource to delete.
    ...    -${ResourceName}: This field specifies type of the resource to delete.
    ...    -${Otheroptions}:This field specifies any kind of options which can be passing into the command.
    Generate String
    Init Action    ${DELETE_RESOURCE}
    Connect to Kubenetes
    Run Keyword If    '${ResourceType}' == 'Invalid'    Action Set    &UC4RB_K8S_RES_TYPE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_K8S_RES_TYPE#    ${ResourceType}
    Run Keyword If    '${ResourceName}' == 'Invalid'    Action Set    &UC4RB_K8S_RES_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_K8S_RES_NAME#    ${ResourceName}
    Run Keyword If    '${Otheroptions}' == 'Invalid'    Action Set    &UC4RB_K8S_OTHER_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_K8S_OTHER_OPTS#    ${Otheroptions}
    Action Execute

Teardown
    Log    *******End Test "DELETE RESOURCE"*******
